#############################所有的tokens
++:Inc
--:Dec
+:Add
-:Sub
*:Star
/:Div
%:Mod
~:Bneg
!:Not
^:Xor
&:Band
(:Lpb
):Rpb
=:Assign
[:Lsb
]:Rsb
{:Lcb
}:Rcb
|:Bor,
;:Sem
":Dq,
':Sq
<,Lt
,:Comma
>,Gt
.,Dot
->:Point
<=:Le
>=:Ge
!=:Ne
==:Eq
&&:Land
||:Lor
+=:Adds
-=:Subs,
*=:Muls
/=:Divs
%=:Mods
<<:Ls
>>:Rs
: :Colon
for:key_for
while:Key_While
if:key_if
else:key_else
switch:key_switch
case:key_case
default:key_default
continue:key_continue
break:key_break
return:key_return
asm:key_asm
void:key_void
byte:key_byte
hword:key_hword
word :key_word
unsigned:key_unsigned
struct:key_struct
#############################下面为c语言bnf子集
<program>   ::={<function_definition>|<declaration>}

<function_definition>   ::=<type_specifier> {"*"} <identifier> "(" [<parameter_list>] ")" <compound_statement>
<parameter_list>  ::= <parameter_declaration> { "," <parameter_declaration> }
<parameter_declaration> ::= <type_specifier> <declarator>
<declaration>     ::= <type_specifier> [ <init_declarator_list> ] ";"
<init_declarator_list> ::= <init_declarator> { "," <init_declarator> }
<init_declarator> ::= <declarator> [ "=" <initializer> ]
<declarator>      ::= {"*"} <direct_declarator>
<direct_declarator> ::= <identifier>
                      | "(" <declarator> ")"
                      | <direct_declarator> "[" <constant> "]"       # 定长数组
<type_specifier>  ::= ["unsigned"] ( "void" | "byte" | "hword" | "word")
                     | "struct" <identifier> [ "{" <struct_declaration_list> "}" ]

<struct_declaration_list> ::= { <struct_declaration> ";" }
<struct_declaration> ::= <type_specifier> <struct_declarator_list>
<struct_declarator_list> ::= <declarator>  { "," <declarator>  }

<initializer>     ::= <assignment_expression>
                     | "{" [<initializer_list>] "}"
<initializer_list> ::= <initializer> { "," <initializer> }
<compound_statement> ::= "{" { <declaration> | <statement> } "}"
<statement>       ::= <expression_statement>
                     | <compound_statement>
                     | <selection_statement>
                     | <iteration_statement>
                     | <jump_statement>
                     | <asm_statement>
<expression_statement> ::= [<expression>] ";"
<selection_statement> ::= "if" "(" <expression> ")" <statement> [ "else" <statement> ]
                        |"switch" "(" <expression> ")" "{" ["case" int_constant|char_constant |"default" ":" <statement> ]   "}"
<iteration_statement> ::= "while" "(" <expression> ")" <statement>
                     | "do" <statement> "while" "(" <expression> ")" ";"
                     | "for" "(" <declaration>|<expression_statement>  <expression_statement>  [<expression>] ")" <statement>
<jump_statement>  ::= "continue" ";"
                     | "break" ";"
                     | "return" [<expression>] ";"
<asm_statement>   ::="asm" "("[<str> {,<str>}]")"
<expression>      ::= <assignment_expression> { "," <assignment_expression> }
<assignment_expression> ::= 
                        <logical_or_expression> [ <assignment_operator> <assignment_expression> ]
<assignment_operator> ::= "=" | "*=" | "/=" | "%=" | "+=" | "-=" 
<logical_or_expression> ::= <logical_and_expression> { "||" <logical_and_expression> }
<logical_and_expression> ::= <inclusive_or_expression> { "&&" <inclusive_or_expression> }
<inclusive_or_expression> ::= <exclusive_or_expression> { "|" <exclusive_or_expression> }
<exclusive_or_expression> ::= <and_expression> { "^" <and_expression> }
<and_expression>  ::= <equality_expression> { "&" <equality_expression> }
<equality_expression> ::= <relational_expression> { ("==" | "!=") <relational_expression> }
<relational_expression> ::= <shift_expression> { ("<" | ">" | "<=" | ">=") <shift_expression> }
<shift_expression> ::= <additive_expression> { ("<<" | ">>") <additive_expression> }
<additive_expression> ::= <multiplicative_expression> { ("+" | "-") <multiplicative_expression> }
<multiplicative_expression> ::= <cast_expression> { ("*" | "/" | "%") <cast_expression> }
<cast_expression> ::= <unary_expression>
                     | "(" <type_specifier> { "*"  } ")" <cast_expression>
<unary_expression> ::= <postfix_expression>
                     | "++" <unary_expression>
                     | "--" <unary_expression>
                     | <unary_operator> <cast_expression>
<unary_operator>   ::= "&" | "*" | "+" | "-" | "~" | "!"
<postfix_expression> ::= <primary_expression>
                     | <postfix_expression> "[" <expression> "]"
                     | <postfix_expression> "(" [<argument_list>] ")"
                     | <postfix_expression> "." <identifier>
                     | <postfix_expression> "->" <identifier>
                     | <postfix_expression> "++"
                     | <postfix_expression> "--"
<primary_expression> ::= <identifier>
                     | <constant>
                     | "(" <expression> ")"
<argument_list>    ::= <assignment_expression> { "," <assignment_expression> }
<constant>         ::= <integer_constant>  | <character_constant>